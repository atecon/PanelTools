set verbose off
clear

set assert stop
include assertion.gfn

include extra.gfn
include "./src/PanelTools.inp" --force


# Helper function
function scalar equal_vectors (matrix y, matrix x)
    /* Check for element-wise equality of both vectors.
    return: scalar, TRUE (=1) of equal at each row, otherwise FALSE (=0). */

    y = vec(y)
    x = vec(x)

    d = abs(y .- x)
    d = selifr(d, ok(d))

    if sumc(d) == 0
        return TRUE
    else
        return FALSE
    endif
end function



open grunfeld.gdt -q -p
genr unit
genr time
smpl unit == 2 && time <= 2 --restrict
series value = NA
smpl unit <= 2 && time <= 4 --restrict --replace --permanent
series expected = {3078.50; 7740.20; 13127.30; 15919.50; 0; 0; 2676.3; 4478.2}

function void test_pcum (const series y, const series expected)
    print "Start testing pcum()"

    # Given
    genr unit

    # When
    series actual = pcum(y)
    series delta = actual - expected

    # Then
    loop i=min(unit)..max(unit)
        smpl unit == $i --restrict --replace
        assert(sum(delta > 0.001) == 0)
    endloop
end function
test_pcum(value, expected)


nulldata 12 --preserve
setobs 3 1:1 --stacked-time-series
series y_with_nan = {1; 2; 3;\
                    3; NA; 1;\
                    NA; 2; 1;\
                    NA; NA; NA\
                    }
series y =          {1; 2; 3;\
                    3; 2; 1;\
                    5; 2; 1;\
                    1; 1; 1\
                    }

series expected_with_nan = {2; 2; 2;\
                            2; 2; 2;\
                            1.5; 1.5; 1.5;\
                            NA; NA; NA\
                           }
series expected =           {2; 2; 2;\
                            2; 2; 2;\
                            2; 2; 2;\
                            1; 1; 1\
                           }

bundles Params = null
Params = Params + _(y = y, value = 0.5, expected = expected)
Params = Params + _(y = y_with_nan, value = 0.5, expected = expected_with_nan)

function void test_pquantile (const bundles P)
    print "Start testing function pquantile()."

    loop foreach i P
        # Given + When
        series expected = P[i].expected
        series actual = pquantile(P[i].y, P[i].value)

        # Then
        assert(equal_vectors({actual}, {expected}) == TRUE)
    endloop
end function
test_pquantile(Params)


nulldata 12 --preserve
setobs 4 1:1 --stacked-time-series
series y = {1; 2; 3; NA;\
            1; 2; NA; NA;\
            NA; NA; NA; NA}
bundles Params = null
Params = Params + _(value = 1, expected = {1; 0; 0; 0;\
                                           1; 0; 0; 0;\
                                           1; 0; 0; 0\
                                           },\
                    fraction = TRUE)
Params = Params + _(value = 2, expected = {0; 1; 0; 0;\
                                           0; 1; 0; 0;\
                                           0; 1; 0; 0\
                                           },\
                    fraction = TRUE)
Params = Params + _(value = -1, expected = {0; 0; 0; 0;\
                                           0; 0; 0; 0;\
                                           0; 0; 0; 0\
                                           },\
                    fraction = TRUE)

Params = Params + _(value = 1, expected = {2; 0; 0; 0;\
                                           2; 0; 0; 0;\
                                           2; 0; 0; 0\
                                           },\
                    fraction = FALSE)
Params = Params + _(value = 2, expected = {0; 2; 0; 0;\
                                           0; 2; 0; 0;\
                                           0; 2; 0; 0\
                                           },\
                    fraction = FALSE)
Params = Params + _(value = -1, expected = {0; 0; 0; 0;\
                                           0; 0; 0; 0;\
                                           0; 0; 0; 0\
                                           },\
                    fraction = FALSE)

function void test_pxfraction (const bundles P, const series y)
    print "Start testing function pxfraction()."

    loop foreach i P
        # Given + When
        series actual = pxfraction(y, P[i].value, P[i].fraction)

        print y actual -o

        # Then
        assert(equal_vectors({actual}, P[i].expected) == TRUE)
    endloop
end function
test_pxfraction(Params, y)


nulldata 12 --preserve
setobs 3 1:1 --stacked-time-series
series y = {1; 2; 3;\
            3; NA; 1;\
            NA; 2; 1;\
            NA; NA; NA\
            }
series expected = {1; 1; 1;\
                    3; 3; 3;\
                    2; 2; 2;\
                    NA; NA; NA\
                    }
function void test_pfirst (const series input, const series expected)
    print "Start testing function pfirst()."

    series actual = pfirst(input)
    series d = actual - expected

    # Then
    assert(sum(abs(d)) == 0)
end function
test_pfirst(y, expected)


nulldata 12 --preserve
setobs 3 1:1 --stacked-time-series
series y = {1; 2; 3;\
            3; NA; 1;\
            NA; 2; 1;\
            NA; NA; NA\
            }
series expected = {3; 3; 3;\
                    1; 1; 1;\
                    1; 1; 1;\
                    NA; NA; NA\
                    }
function void test_plast (const series input, const series expected)
    print "Start testing function plast()."

    series actual = plast(input)
    series d = actual - expected

    # Then
    assert(sum(abs(d)) == 0)
end function
test_plast(y, expected)


nulldata 12 --preserve
setobs 3 1:1 --stacked-time-series
series y = {1; 2; 3;\
            3; NA; 1;\
            NA; 2; 1;\
            NA; NA; NA\
            }
series expected = {2; 2; 5/3;\
                   2; 2; 5/3;\
                   2; 2; 5/3;\
                   2; 2; 5/3\
                   }
function void test_pxmean (const series input, const series expected)
    print "Start testing function pxmean()."

    series actual = pxmean(input)
    series d = actual - expected

    # Then
    assert(sum(abs(d)) == 0)
end function
test_pxmean(y, expected)


nulldata 12 --preserve
setobs 3 1:1 --stacked-time-series
series y = {1; 2; 3;\
            3; 1; 1;\
            NA; 2; 2;\
            NA; NA; NA\
            }
series expected = {3; 3; 3;\
                    1; 1; 1;\
                    2; 2; 2;\
                    NA; NA; NA\
                    }
function void test_pmode (const series input, const series expected)
    print "Start testing function pmode()."

    # Given + When
    series actual = pmode(input)
    series d = actual - expected

    # Then
    assert(sum(abs(d)) == 0)
end function
test_pmode(y, expected)

# TODO: Could test the printed output
open grunfeld.gdt -q -p
panelinfo()



nulldata 8 --preserve
setobs 4 1:1 --stacked-time-series

bundles Params = null
Params = Params + _(y = {1; 2; 3; 4; 2; 2; 2; 2},\
                    expected = {1; 2; 3; 4; 2; 2; 2; 2},\
                    order = 1)
Params = Params + _(y = {1; 2; 4; 8; 2; 4; 8; 16},\
                    expected = {0.3; 2.6; 4.9; 7.2; 0.6; 5.2; 9.8; 14.4},\
                    order = 2)
Params = Params + _(y = {1; 2; 3; 4; 2; 2; 2; 2},\
                    expected = {1; 2; 3; 4; 2; 2; 2; 2},\
                    order = 3)

Params = Params + _(y = {NA; 2; 3; 4; 2; 2; 2; 2},\
                    expected = {NA; NA; NA; NA; 2; 2; 2; 2},\
                    order = 1)
Params = Params + _(y = {1; 2; 4; NA; 2; 4; 8; 16},\
                    expected = {NA; NA; NA; NA; 0.6; 5.2; 9.8; 14.4},\
                    order = 2)
function void test_ppolyfit (const bundles P)
    print "Start testing function ppolyfit()."

    loop foreach i P
        printf "\nCase = %d\n", $i

        # Given
        series y = P[i].y
        series expected = P[i].expected

        # When
        series actual = ppolyfit(y, P[i].order)

        # Then
        assert_almost_equal_num(sum(actual - expected), 0, 10e-15)
    endloop
end function
test_ppolyfit(Params)


bundles Params = null
Params = Params + _(N = 2, input = {1; 3; -5; -10},\
                    expected = {2; 2; -7.5; -7.5})
Params = Params + _(N = 2, input = {1; NA; -5; -10},\
                    expected = {1; 1; -7.5; -7.5})
Params = Params + _(N = 2, input = {NA; NA; -5; -10},\
                    expected = {NA; NA; -7.5; -7.5})

function void test_mpmean (const bundles P)
    print "Start testing function mpmean()."

    loop foreach i P
        # Given + When
        matrix actual = mpmean(P[i].input, P[i].N)

        # Then
        loop idx=1..rows(actual)
            assert(ok(actual[idx]) == ok((P[i].expected[idx])))
            if ok(actual[idx])
                assert(actual[idx] == P[i].expected[idx])
            endif
        endloop

    endloop
end function
test_mpmean(Params)


bundles Params = null

Params = Params + _(input = {1, 1, 1, 1;\
                             2, 1, NA, NA;\
                             3, 1, 1, 2},\
                    expected = {1, 1;\
                                2, NA;\
                                3, 2}\
                    )
function void test_drop_constants (const bundles P)
    print "Start testing function drop_constants()."

    loop foreach i P
        # When
        matrix actual = drop_constants(P[i].input)

        # Then
        loop j=1..cols(actual)
            loop k=1..rows(actual)
                if ok(actual[k,j]) && ok(P[i].expected[k,j])
                    assert(actual[k,j] == P[i].expected[k,j])
                endif
            endloop
        endloop
    endloop
end function
test_drop_constants(Params)



bundles Params = null
Params = Params + _(N = 2, T = 3, coeff = {0; 1},\
                    ybar = {1; 1; 1; 2; 2; 2},\
                    xbar = {0, 2; 0, 2; 0, 2; 2, 4; 2, 4; 2, 4; 2, 4},\
                    expected = {-1; -1; -1; -2; -2; -2} )

function void test_get_ahat (const bundles P)
    print "Start testing function get_ahat()."

    loop foreach i P
        # When
        matrix actual = get_ahat(P[i].ybar, P[i].xbar, P[i])

        # Then
        loop idx=1..rows(actual)
            assert(ok(actual[idx]) == ok((P[i].expected[idx])))
            if ok(actual[idx])
                assert(actual[idx] == P[i].expected[idx])
            endif
        endloop

    endloop
end function
test_get_ahat(Params)



open grunfeld.gdt -p -q
genr unit
smpl unit <= 2 --restrict --permanent
panel invest const kstock value --fixed-effects --quiet
bundle Model = $model
Model.ahat = vec(mshape({$ahat}, $pd, $nobs/$pd)[,1])

function void test_fe_estimator (const list L, const bundle Model)
    print "Start testing function fe_estimator()"

    # Given
    matrix y = {L.invest}
    list Z = const L.kstock L.value
    matrix X = {Z}
    Z_wo_const = Z - const
    strings expected_parnames = varnames(Z_wo_const)

    # When
    bundle actual = fe_estimator(y, X, $nobs/$pd)

    # Then
    loop i=2..rows(Model.coeff)
        assert_almost_equal_num(Model.coeff[i], actual.coeff[i-1], 10e-5)
    endloop
    loop i=1..rows(Model.ahat)
        assert_almost_equal_num(Model.ahat[i], actual.ahat[i], 10e-5)
    endloop

    loop foreach i expected_parnames
        assert(actual.parnames[i] == "$i")
    endloop
end function
test_fe_estimator(dataset, Model)



open grunfeld.gdt -p -q
genr unit
smpl unit <= 2 --restrict --permanent
panel invest const kstock value --pooled #--quiet
bundle Model = $model

function void test_pols_estimator (const list L, const bundle Model)
    print "Start testing function pols_estimator()"

    # Given
    matrix y = {L.invest}
    list Z = const L.kstock L.value
    matrix X = {Z}
    strings expected_parnames = varnames(Z)

    # When
    bundle actual = pols_estimator(y, X, $nobs/$pd)

    # Then
    loop i=1..rows(Model.coeff)
        assert_almost_equal_num(Model.coeff[i], actual.coeff[i], 10e-5)
    endloop

    loop foreach i expected_parnames
        assert(actual.parnames[i] == "$i")
    endloop
end function
test_pols_estimator(dataset, Model)


bundles Params = null
Params = Params + _(y = {1, 2, 3, 4}', x = {1, 2, 3, 4}',\
                    expected = 1)
Params = Params + _(y = {1, 2, 3, 4}', x = {-1, -2, -3, -4}',\
                    expected = 1)
Params = Params + _(y = {1, 2, 3, 4}', x = {0, 0, 0, 0}',\
                    expected = 0)
function void test_rsq_overall (const bundles P)
    print "Start testing rsq_overall()"

    loop foreach i P
        # Given + When
        scalar actual = rsq_overall(P[i].y, P[i].x)

        # Then
        assert(actual == P[i].expected)
    endloop
end function
test_rsq_overall(Params)


function void test_number_of_droppped_cols ()
    print "Start testing print_number_of_droppped_cols()"

    # Given
    scalar x = 4
    scalar y = 2
    string actual = ""
    string expected = sprintf("INFO: Dropped %d columns from feature list.\n", (x - y))

    # When
    outfile --buffer=actual
        print_number_of_droppped_cols(x, y)
    end outfile

    # Then
    assert(actual == expected)
end function
test_number_of_droppped_cols()


bundles Params = null
Params = Params + _(S = defarray("A", "B", "C"), mask = {1, 1, 1},\
                    expected = defarray("A", "B", "C"))
Params = Params + _(S = defarray("A", "B", "C"), mask = {0, 0, 0},\
                    expected = array(0))
Params = Params + _(S = defarray("D"), mask = {1, 1, 1},\
                    expected = array(0))
function void test_mask_string_array (const bundles P)
    print "Start testing mask_string_array()"

    loop foreach i P
        # Given + When
        strings actual = mask_string_array(P[i].S, P[i].mask)

        # Then
        if nelem(P[i].expected)
            loop foreach j P[i].expected
                assert(actual[j] == P[i].expected[j])
            endloop
        endif
    endloop
end function
test_mask_string_array(Params)


bundles Params = null
Params = Params + _(m = mnormal(3,2),\
                    cnames = defarray("A", "B"),\
                    S = defarray("A", "B", "C"),\
                    expected = defarray("A", "B")\
                    )
Params = Params + _(m = mnormal(3,2),\
                    cnames = defarray("A", "B"),\
                    S = defarray("C"),\
                    expected = array(0)\
                    )

function void test_parnames_to_matrix (const bundles P)
    print "Start testing parnames_to_matrix()"

    loop foreach i P
        # Given
        matrix m = P[i].m
        cnameset(m, P[i].cnames)

        # When
        matrix mout = parnames_to_matrix(m, P[i].S)
        strings actual = cnameget(mout)

        # Then
        if nelem(P[i].expected)
            loop foreach j P[i].expected
                assert(actual[j] == P[i].expected[j])
            endloop
        endif
    endloop
end function
test_parnames_to_matrix(Params)


function void test_model_bundle_basics (void)
    print "Start testing function model_bundle_basics()"

    # Given
    matrix y = {1, 2, 3, 4, 5, 6}'
    matrix X = mnormal(6, 2)
    scalar N = 2
    string NAME_Y = "endog"
    strings NAMES_X = defarray("exog1", "exog 2")
    cnameset(y, NAME_Y)
    cnameset(X, NAMES_X)

    # When
    bundle expected = _(N = N,\
                        T = rows(y) / N,\
                        depvar = NAME_Y,\
                        parnames = NAMES_X,\
                        error = FALSE)

    bundle actual = model_bundle_basics(y, X, N)

    # Then
    loop foreach i expected
        if typestr(typeof(actual["$i"])) == "array"
            continue
        else
            assert(actual["$i"] == expected["$i"])
        endif
    endloop
end function
test_model_bundle_basics()



# ===============
## NOT TESTED YET
# ===============
function void test_pfcast (void)
    print "Start testing function pfcast()"

    # Given

    # When

    # Then
end function
test_pfcast()


function void test_meanc_robust (void)
    print "Start testing function meanc_robust()"

    # Given

    # When

    # Then
end function
test_meanc_robust()


function void test_sdc_robust (void)
    print "Start testing function sdc_robust()"

    # Given

    # When

    # Then
end function
test_sdc_robust()


function void test_get_description (void)
    print "Start testing function get_description()"

    # Given

    # When

    # Then
end function
test_get_description()


function void test_helper_pfirst_plast (void)
    print "Start testing function helper_pfirst_plast()"

    # Given

    # When

    # Then
end function
test_helper_pfirst_plast()


print "Finished all tests succesfully."

